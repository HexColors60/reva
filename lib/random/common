| vim: ft=reva :
| random number generator common words

push~
: os-rand ms@ ;
LIN [IF]
: os-rand 
	" /dev/urandom" open/r
	ioerr @ 0if
		| h
		dup here 4 rot read drop close
		here @
	else
		drop prior os-rand
	then
	;
[THEN]

defer rand
: choose  ( n -- 0..n-1 )  rand  in~ ~doubles um*  nip  ;
: choose[]  ( lo hi -- lo..hi )  1+  over -  choose  +  ;
| chooser: choose range
| select a random number between lo and hi-1 inclusive
| e.g. 1 3 chooser can return either 1 or 2
: chooser ( lo hi -- lo..hi-1) 1- choose[] ;
pop~

|||

def: choose
stack: n -- 0..n-1
ctx: ~
desc: =
	Select a number between zero and n-1 inclusive.
=

def: choose[]
stack: lo hi -- lo..hi
ctx: ~
desc: =
	Select a number between lo and hi inclusive.
=

def: chooser
stack: lo hi -- lo..hi-1
ctx: ~
desc: =
	Select a number between lo and hi-1 inclusive.
=
